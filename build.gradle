buildscript {
    ext {
        springBootVersion = "3.0.0"
        springBootNativeVersion = "0.9.18"
        springCloudVersion = "2022.0.0"
        springCloudAlibabaVersion = "2022.0.0.0-RC1"
        findbugsVersion = "3.0.2"
        lombokVersion = "1.18.24"
        hutoolVersion = "5.8.11"
        ip2regionVersion = "1.7.2"
        mybatisPlusVersion = "3.5.3.1"
        dynamicDatasourceVersion = "3.6.0"
        p6spyVersion = "3.9.1"
        knife4jVersion = "4.1.0"
        apmToolkitLogbackVersion = "8.7.0"
        easyCaptchaVersion = "1.6.2"
        seataVersion = "1.6.1"
        minioVersion = "8.0.3"
        okhttpVersion = "4.9.0"
        jibVersion = "3.3.1"
        authorizationServerVersion = "1.0.0"
        guavaVersion = "31.1-jre"
        easyCaptchaVersion = "1.6.2"
        springDocVersion = "2.0.2"
        jasyptVersion = "3.0.5"
        lombokMapstructBindingVersion = "0.2.0"
        guavaVersion = "31.1-jre"
        javassistVersion = "3.28.0-GA"
        redissonVersion = "3.20.0"
        xxlJobVersion = "2.4.0"
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
        mavenCentral()
    }
}

plugins {
    id "base"
    id "org.springframework.boot" version "$springBootVersion" apply false
    id "org.graalvm.buildtools.native" version "$springBootNativeVersion" apply false
    id "com.google.cloud.tools.jib" version "$jibVersion" apply false
}

ext {
    javaVersion = JavaVersion.VERSION_17
    bom = Set.of(project(":cloud-bom"), project(":common"),
            project(":platform"), project(":platform:platform-system-bom"),
            project(":test"), project(":test:test-test-bom"))
    jibPackage = Set.of(project(":platform:platform-auth"),
            project(":platform:platform-gateway"),
            project(":platform:platform-system-bom:platform-system"),
            project(":test:test-test-bom:test-test"),
            project(":third-part-database-sync"))
    nativePackage = Set.of(project(":platform:platform-auth"),
            project(":platform:platform-gateway"),
            project(":platform:platform-system-bom:platform-system"))
    commons = (subprojects - bom)
}

subprojects {
    group = group
    version = version
}

configure(commons) {
    apply plugin: "java-library"
    sourceCompatibility = "$javaVersion"
    targetCompatibility = "$javaVersion"
    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    configurations {
        management {
            canBeConsumed = false
            canBeResolved = false
            visible = false
        }
        compileClasspath.extendsFrom(management)
        testCompileClasspath.extendsFrom(management)
        runtimeClasspath.extendsFrom(management)
        testRuntimeClasspath.extendsFrom(management)
        annotationProcessor.extendsFrom(management)
        testAnnotationProcessor.extendsFrom(management)
    }

    dependencies {
        // import BOM.
        management platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        management platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")
        management platform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:$springCloudAlibabaVersion")
        management platform("com.github.xiaoymin:knife4j-dependencies:$knife4jVersion")
        management platform(project(":cloud-bom"))
        management platform(project(":common"))
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:$jasyptVersion"
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    compileJava.options.compilerArgs.add '-parameters'
    compileTestJava.options.compilerArgs.add '-parameters'
    compileJava.dependsOn(processResources)
}

configure(jibPackage) {
    apply plugin: "java"
    apply plugin: "com.google.cloud.tools.jib"

    jib {
        from {
            image = "${dockerJibFromImage}"
            auth {
                username = "${dockerJibAuthUsername}"
                password = "${dockerJibAuthPassword}"
            }
        }
        to {
            image = "${dockerJibToImage}/${project.name}"
            tags = ["${version}", "latest"]
            auth {
                username = "${dockerJibAuthUsername}"
                password = "${dockerJibAuthPassword}"
            }
        }
        allowInsecureRegistries = true
        container {
            environment = ["SW_AGENT_NAME": project.name, "TZ": "Asia/Shanghai"]
            entrypoint = "INHERIT"
            format = "OCI"
            creationTime = "USE_CURRENT_TIMESTAMP"
        }
    }
}

//configure(nativePackage) {
//    apply plugin: "org.graalvm.buildtools.native"
//
//}